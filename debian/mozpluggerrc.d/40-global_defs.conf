# Configure file for MozPlugger 1.14.2
# Version: Sept 18, 2010
#
# Commands which are not installed on your system will not be used.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111, USA.


###################
### m4 macros   ###
###################

changequote([,])

### Helpers

### MPlayer

define(MP_CMD,[mplayer -really-quiet -nojoystick -nofs -zoom -vo xv,x11 -ao esd,alsa,oss,arts,null -osdlevel 0 $1 </dev/null])

define(MP_EMBED,[embed noisy ignore_errors: MP_CMD(-xy $width -wid $window $1)])

define(MP_NOEMBED,[noembed noisy ignore_errors maxaspect swallow(MPlayer): MP_CMD($1)])

# If you want a small controls in top left corner of embedded window when using
# mplayer then uncomment the first and comment the second below 

#define(MP_VIDEO_STREAM,[controls stream MP_EMBED($1 "$file")
#	stream MP_NOEMBED($1 "$file")])

define(MP_VIDEO_STREAM,[stream MP_EMBED($1 "$file")
	stream MP_NOEMBED($1 "$file")])

define(MP_VIDEO_PLAYLIST, [fmatch($1) MP_EMBED(-playlist "$file")
	                   fmatch($1) MP_NOEMBED(-playlist "$file")])
# Special case for Quicktime
define(MP_LINKS,[stream links noisy ignore_errors: MP_CMD($1)])

define(MP_AUDIO,[mplayer -really-quiet -nojoystick $1 </dev/null])

define(MP_AUDIO_STREAM,[controls stream noisy ignore_errors: mplayer -really-quiet -nojoystick $1 "$file" </dev/null])

### Totem

define(TM_CMD,[totem $1</dev/null])

define(TM_EMBED,[embed noisy ignore_errors fill swallow(Totem): TM_CMD(--toggle-controls $1)])

define(TM_NOEMBED,[nokill noembed noisy ignore_errors: TM_CMD($1)])


define(TM_VIDEO_STREAM,[stream TM_EMBED("$file")
	stream TM_NOEMBED("$file")])

# Special case for Quicktime
define(TM_LINKS,[stream links nokill noembed noisy ignore_errors: TM_CMD($1)])

define(TM_AUDIO_STREAM,[controls stream noisy ignore_errors: TM_CMD("$file")])

### OpenOffice
define([OO],[swallow(VCLSalFrame) fill: ooffice2.0 -nologo -norestore -view $1 "$file"
	swallow(VCLSalFrame) fill: ooffice -nologo -norestore -view $1 "$file"
	swallow(VCLSalFrame) fill: soffice -nologo $1 "$file"])

### Acrobat Reader
define(ACROREAD, [repeat swallow(acroread) fill : acroread -openInNewWindow /a "$fragment" "$file"])

### GV
define(GV_OPTS,[--safer --quiet --antialias -geometry +9000+9000])
define(GV_FLAGS,[repeat noisy swallow(gv) fill])
define(GV,[GV_FLAGS(): gv GV_OPTS() "$file"])

